<!DOCTYPE html>
<html>
<head>
    <meta http-equiv="content-type" content="text/html;charset=UTF-8">
    <title>Snakes</title>
    <style type="text/css">
        body {
            margin: 0;
        }
        #container {
            position: relative;
            top: 20px;
            text-align: center;
        }

        #content {
            border: 1px solid black;
            vertical-align: middle;
        }
    </style>
    <script src="http://code.jquery.com/jquery-1.8.1.min.js"></script>
    <script src="http://underscorejs.org/underscore-min.js"></script>
    <script type="text/javascript">
        // prepare animation loop utility

        window.requestAnimFrame = (function (callback) {
            return window.requestAnimationFrame ||
                window.webkitRequestAnimationFrame ||
                window.mozRequestAnimationFrame ||
                window.oRequestAnimationFrame ||
                window.msRequestAnimationFrame ||
                function (callback) {
                    window.setTimeout(callback, 1000 / 60);
                };
        })();
    </script>
</head>
<body>
<div id="container">
    <canvas id="content"></canvas>
    <br/>
    <button id="start">Start</button>
</div>

<script type="text/coffeescript">

    # constants
    MAX_WIDTH = 1200
    MAX_HEIGHT = 800
    BLOCK_SIZE = 25
    REFRESH_RATE = 150 # ms

    # global vars
    stage = document.getElementById "content"
    ctx = stage.getContext "2d"
    running = false
    nextUpdate = 0
    appleImage = null

    # instances
    snake = null
    apple = null

    prepareCanvas = ->
        stage.width = MAX_WIDTH
        stage.height = MAX_HEIGHT


    start = ->
        running = true
        animLoop = ->
            requestAnimFrame ->
                animate()
                animLoop() if running

        animLoop()

    animate = ->
        try
            if $.now() >= nextUpdate
                update()
                render()
        catch e
            running = false
            console.log e
            console.log e.stack if e.stack?

    update = ->
        snake.move()

        # what if the snake hits something: wall, itself, apple

    render = ->
        # draw
        ctx.clearRect 0, 0, MAX_WIDTH, MAX_HEIGHT

        apple.draw ctx
        snake.draw ctx

        nextUpdate = $.now() + REFRESH_RATE

    createSnake = ->
        snake = new Snake()

    addApple = ->
        {x, y} = snake.getHead()

        apple = new Apple x, y
        apple.x = randomInt(MAX_WIDTH / BLOCK_SIZE) while apple.x == x
        apple.y = randomInt(MAX_HEIGHT / BLOCK_SIZE) while apple.y == y


    randomInt = (n) ->
      Math.floor Math.random() * n


    getStartButton = -> document.getElementById "start"

    gameOver = ->
        getStartButton().disabled = ""
        running = false

    loadApple = ->
        img = new Image()
        img.onload = -> appleImage = img
        img.src = 'http://valuestream2009.files.wordpress.com/2012/01/apple-logo-red.jpg'

    prepareButton = ->
        startButton = getStartButton()
        startButton.onclick = ->
            createSnake()
            addApple()
            start()
            startButton.disabled = 'disabled'

    bindKeyboard = ->
        $(document).keydown (e) ->
            direction =
                switch e.keyCode
                    when 38 then 'up'
                    when 40 then 'down'
                    when 37 then 'left'
                    when 39 then 'right'
                    else null

            snake?.direction = direction if direction?

    do ->
        prepareCanvas()
        prepareButton()
        bindKeyboard()
        loadApple()

    class Block
        constructor: (@x, @y) ->
        up: -> new Block @x, @y - 1
        down: -> new Block @x, @y + 1
        left: -> new Block @x - 1, @y
        right: -> new Block @x + 1, @y


    class Snake
        constructor: ->
            @direction = 'right'
            @blocks = [
                new Block 3, 0
                new Block 2, 0
                new Block 1, 0
            ]
            @length = @blocks.length
            @color = "green"

        getHead: ->
            _.first @blocks

        move: ->
            @blocks.pop() while @blocks.length > @length - 1
            @blocks.unshift @getHead()[@direction]()

        draw: (ctx) ->
            @blocks.forEach ({x, y}) ->
                ctx.save()
                ctx.fillStyle = @color ? "green"
                ctx.fillRect x * BLOCK_SIZE + 1, y * BLOCK_SIZE + 1, BLOCK_SIZE - 2, BLOCK_SIZE - 2
                ctx.restore()


    class Apple
        constructor: (@x, @y) ->

        draw: (ctx) ->
            ctx.save()
            if appleImage?
                ctx.translate @x * BLOCK_SIZE + 1, @y * BLOCK_SIZE + 1
                ctx.scale (BLOCK_SIZE - 2) / appleImage.width, (BLOCK_SIZE - 2) / appleImage.height
                ctx.drawImage appleImage, 0, 0
            else
                ctx.beginPath()
                ctx.fillStyle = "red"
                ctx.arc @x * BLOCK_SIZE + BLOCK_SIZE / 2, @y * BLOCK_SIZE + BLOCK_SIZE / 2, BLOCK_SIZE / 2, 0, 2 * Math.PI, false
                ctx.fill()
            ctx.restore()


</script>

<script src="http://coffeescript.org/extras/coffee-script.js"></script>
</body>
</html>