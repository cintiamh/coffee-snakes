<!DOCTYPE html>
<html>
<head>
    <meta http-equiv="content-type" content="text/html;charset=UTF-8">
    <title>Snakes</title>
    <style type="text/css">
        body {
            margin: 0;
        }
        #container {
            position: relative;
            top: 20px;
            text-align: center;
        }

        #content {
            border: 1px solid black;
            vertical-align: middle;
        }
    </style>
    <script src="http://code.jquery.com/jquery-1.8.1.min.js"></script>
    <script src="http://underscorejs.org/underscore-min.js"></script>
    <script type="text/javascript">
        // prepare animation loop utility

        window.requestAnimFrame = (function (callback) {
            return window.requestAnimationFrame ||
                window.webkitRequestAnimationFrame ||
                window.mozRequestAnimationFrame ||
                window.oRequestAnimationFrame ||
                window.msRequestAnimationFrame ||
                function (callback) {
                    window.setTimeout(callback, 1000 / 60);
                };
        })();
    </script>
</head>
<body>
<div id="container">
    <canvas id="content"></canvas>
    <br/>
    <button id="start">Start</button>
</div>

<script type="text/coffeescript">
   # constants
   MAX_WIDTH = 1200
   MAX_HEIGHT = 800
   BLOCK_SIZE = 25
   REFRESH_RATE = 150 # ms

   # global vars
   stage = document.getElementById "content"
   ctx = stage.getContext "2d"
   running = false
   nextUpdate = 0
   snake = null

   prepareCanvas = ->
       stage.width = MAX_WIDTH
       stage.height = MAX_HEIGHT


   start = ->
       running = true
       animLoop = ->
           requestAnimFrame ->
               animate()
               animLoop() if running

       animLoop()

   animate = ->
       try
           if $.now() >= nextUpdate
               update()
               render()
       catch e
           running = false
           console.log e
           console.log e.stack if e.stack?

   update = ->
       snake.move()

   render = ->
       # draw
       ctx.clearRect 0, 0, MAX_WIDTH, MAX_HEIGHT
       snake.blocks.forEach ({x, y}) ->
           ctx.save()
           ctx.fillStyle = snake?.color ? "green"
           ctx.fillRect x * BLOCK_SIZE + 1, y * BLOCK_SIZE + 1, BLOCK_SIZE - 2, BLOCK_SIZE - 2
           ctx.restore()

       nextUpdate = $.now() + REFRESH_RATE

   createSnake = ->
       new Snake()

   newGame = ->
       snake = createSnake()

   getStartButton = -> document.getElementById "start"

   gameOver = ->
       getStartButton().disabled = ""
       running = false

   prepareButton = ->
       startButton = getStartButton()
       startButton.onclick = ->
           newGame()
           start()
           startButton.disabled = "disabled"

   bindKeyboard = ->
       $(document).keydown (e) ->
           direction =
               switch e.keyCode
                   when 38 then 'up'
                   when 40 then 'down'
                   when 37 then 'left'
                   when 39 then 'right'
                   else null

           snake?.direction = direction if direction?

   do ->
       prepareCanvas()
       prepareButton()
       bindKeyboard()

   class Block
       constructor: (@x, @y) ->
       up: -> new Block @x, @y - 1
       down: -> new Block @x, @y + 1
       left: -> new Block @x - 1, @y
       right: -> new Block @x + 1, @y


   class Snake
       constructor: ->
           @direction = 'right'
           @blocks = [
               new Block 3, 0
               new Block 2, 0
               new Block 1, 0
           ]
           @color = "green"

       getHead: ->
           _.first @blocks

       move: ->
           @blocks.pop()
           @blocks.unshift @getHead()[@direction]()


</script>

<script src="http://coffeescript.org/extras/coffee-script.js"></script>
</body>
</html>